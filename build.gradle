plugins {
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

wrapper {
    gradleVersion = '7.6.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

ext.libraries = [
    project(':squashfs-tools'),
]

configure(libraries) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withSourcesJar()
        withJavadocJar()
    }
}

configure(subprojects.findAll {it !in libraries}) {
    apply plugin: 'java'
}

subprojects {
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        maven {
            url 'https://mvn.slimjars.com'
        }
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

if (project.hasProperty("publish")) {
    nexusPublishing {
        repositories {
            sonatype {
                nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
                snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
                username = sonatype_username
                password = sonatype_password
            }
        }
    }
}
